buildscript {
    ext.kotlin_version = '1.1.51'
    ext.jUnit_platform_gradle_plugin_version = '1.0.0'
    ext.mockito_kotlin_version = '1.5.0'
    ext.spek_version = '1.1.5'
    ext.kluent_version = '1.30'
    ext.ktlint_version = '0.11.1'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.junit.platform:junit-platform-gradle-plugin:$jUnit_platform_gradle_plugin_version"
    }
}

apply plugin: 'org.junit.platform.gradle.plugin'

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'

mainClassName = 'com.example.Calculator'

defaultTasks 'test'

repositories {
  jcenter()
}

configurations {
    ktlint
}

dependencies {
  ktlint "com.github.shyiko:ktlint:$ktlint_version"
  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
  testCompile "com.nhaarman:mockito-kotlin:$mockito_kotlin_version"
  testCompile "org.jetbrains.spek:spek-api:$spek_version"
  testRuntime "org.jetbrains.spek:spek-junit-platform-engine:$spek_version"
  testCompile "org.amshove.kluent:kluent:$kluent_version"
  testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/shyiko/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

task wrapper(type: Wrapper) {
  gradleVersion = "4.1"
}
